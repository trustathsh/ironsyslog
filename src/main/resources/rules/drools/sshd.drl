import function de.hshannover.f4.trust.ironsyslog.util.DroolsRuleLogger.debug
import de.hshannover.f4.trust.ironsyslog.ep.events.LoginFailedEvent
import de.hshannover.f4.trust.ironsyslog.ep.events.IronSyslogServerEvent
import java.util.regex.Matcher
import java.util.regex.Pattern

declare LoginFailedEvent
@role(event)
@timestamp(timestamp)
end

declare IronSyslogServerEvent
@role(event)
@timestamp(timestamp)
end

rule "sshd login failed"
when
	$ise:IronSyslogServerEvent(message.contains("sshd"), message.contains("Failed password"))
then
	debug("Rule \"" + drools.getRule().getName() + "\" fired.");
	String message = $ise.getMessage();
	Pattern p;
	boolean isValidUser;
	if(message.contains("invalid user")) {
		isValidUser = false;
		p = Pattern.compile("(sshd)\\[\\d+\\]: Failed password for invalid user (.*) from (\\d{1,3}\\.\\d{1,3}\\.\\d{1,3}\\.\\d{1,3}) port (\\d{1,5}) (.*)");
	} else {
		isValidUser = true;
		p = Pattern.compile("(sshd)\\[\\d+\\]: Failed password for (.*) from (\\d{1,3}\\.\\d{1,3}\\.\\d{1,3}\\.\\d{1,3}) port (\\d{1,5}) (.*)");
	}
	Matcher m = p.matcher(message);
	if (m.find()) {
		String serviceName = m.group(1);
		String userId = m.group(2);
		String userIp = m.group(3);
		insert ( new LoginFailedEvent(userId, userIp, $ise.getHost(), serviceName, isValidUser) );
	}
end
